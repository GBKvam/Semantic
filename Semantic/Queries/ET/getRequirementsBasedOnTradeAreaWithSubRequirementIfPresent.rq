PREFIX : <https://www.sdir.no/SDIR_Simulator#> 
PREFIX scope: <https://www.sdir.no/SDIR_Simulator/shapes/scope#> 
PREFIX unit: <http://qudt.org/vocab/unit/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

# Return information about a requirement based on specific trade area.
# Including information about sub requirements.
# See comments in query for changing magic strings/numbers to variables in some API-call or similar.

SELECT ?paragraph ?label ?tradeAreaLabel ?regulationReference ?theme ?subRequirement ?subRequirementLabel ?subRequirementRegulationReference ?subRequirementTheme
WHERE {
  ?paragraph a :Requirement .
  
  # If trade area scope are connected directly to subject
  OPTIONAL {
    ?paragraph sh:property ?scope .

    ?scope sh:path :tradeArea ;
                 sh:hasValue ?tradeArea . # Change ?tradeArea with any trade area e.g. :BankFishingI

    ?tradeArea rdfs:label ?tradeAreaLabel .
  }
  
  # If trade area scope are in an alternative list
  OPTIONAL {
  	?paragraph sh:or/rdf:rest*/rdf:first ?orItem .
    ?orItem sh:and/rdf:rest*/rdf:first ?scope .
    
    ?scope sh:path :tradeArea ;
                 sh:hasValue ?tradeArea . # Change ?tradeArea with any trade area e.g. :BankFishingI

    ?tradeArea rdfs:label ?tradeAreaLabel .
  }
  FILTER(LANG(?tradeAreaLabel) = 'no')
  
  # Get label in given language
  ?paragraph rdfs:label ?label .
  FILTER(LANG(?label) = 'no') # Insert language tag for label ('no' or 'en')
  
  # Regulation reference URL
  ?paragraph :regulationReference ?regulationReference .
  
  # If present, return information about sub-requirements
  OPTIONAL { 
    ?paragraph :subRequirement ?subRequirement . 
    
    # Get label in given language
    ?subRequirement rdfs:label ?subRequirementLabel .
    FILTER(LANG(?subRequirementLabel) = 'no') # Insert language tag for label ('no' or 'en')

    # Regulation reference URL
    ?subRequirement :regulationReference ?subRequirementRegulationReference .

    # Get tag in given language
    ?subRequirement :theme ?subRequirementTheme .
    FILTER(LANG(?subRequirementTheme) = 'no') # Insert language tag for label ('no' or 'en') 
  }
  
  # Get tag in given language
  ?paragraph :theme ?theme .
  FILTER(LANG(?theme) = 'no') # Insert language tag for label ('no' or 'en') 
  
}